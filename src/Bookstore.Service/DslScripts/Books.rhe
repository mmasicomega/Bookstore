Module Bookstore
{
    Entity Book
    {
        ShortString Code { AutoCode; DefaultValue 'item => "BID+++++++"'; DenyUserEdit; }
        ShortString Title { Required; }
        Integer NumberOfPages { MaxValue 5000; MinValue 1; }
        Reference Author Bookstore.Person;

        DateTime TimeCreated { CreationTime; }

        Logging { AllProperties; }
    }

    Entity Person
    {
        ShortString Name;
    }

    Entity Comment
    {
        Reference Book { Detail; }
        LongString Text;
        DateTime TimeModified { ModificationTimeOf Bookstore.Comment.Text; }
        Logging { Log Bookstore.Comment.Text; }
    }

    Entity ChildrensBook
    {
        Extends Bookstore.Book;

        Integer AgeFrom;
        Integer AgeTo;
        IntegerRange AgeFrom AgeTo;
    }

    Entity ForeignBook
    {
        Extends Bookstore.Book;

        ShortString OriginalLanguage;
        Reference Translator Bookstore.Person;
    }

    Entity Topic
    {
        ShortString Name { Unique; Required; }
    }

    Entity BookTopic
    {
        Reference Book { Detail; }
        Reference Topic { Required; }

        UniqueMultiple 'Book Topic';
    }

// ---------------------------------------------------------------

    Entity Employee
    {
        ShortString Name;
        ShortString VATNumber { Unique; Required; }
    }

    Entity Manager
    {
        Extends Bookstore.Employee;

        LongString BonusInformation;
    }

    Entity EducationRecord
    {
        Reference Employee Bookstore.Employee;

        Date DateAcquired;

        LongString Description;
    }

    Entity Department
    {
        ShortString Code { Unique; Required; }
        LongString Description;

        Deactivatable;

        ItemFilter ActiveLocked 'item => item.Active.Value';
        Lock ActiveLocked 'Active departments cannot be edited.';
    }

    Entity DepartmentEmployee
    {
        Reference Department { Required; }
        Reference Employee { Required; }

        UniqueMultiple 'Department Employee';
    }
    
}